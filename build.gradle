buildscript {
    apply from: 'versions.gradle'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
        classpath "com.alibaba:arouter-register:$routerRegisterVersion"

        classpath "com.google.gms:google-services:$googleServicesVersion"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$firebaseCrashlyticsGradleVersion"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.1.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint().userData([
                    'max_line_length': '120',
            ])
            licenseHeaderFile project.rootProject.file('copyright.kt')
        }
    }

    tasks.whenTaskAdded {
        if (name == 'preBuild') {
            mustRunAfter 'spotlessCheck'
        }
    }
}

task initGitHooks {
    ['sh', '-c', 'git config core.hooksPath .githooks'].execute().text.trim()
}

tasks.getByPath(':app:preBuild').dependsOn initGitHooks
