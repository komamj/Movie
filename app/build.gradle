apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'dagger.hilt.android.plugin'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

apply from: '../shared_dependencies.gradle'
apply from: '../test_dependencies.gradle'

kapt {
    correctErrorTypes true
}

android {
    compileSdkVersion defaultCompileSdkVersion as Integer
    buildToolsVersion defaultBuildToolsVersion as String

    defaultConfig {
        applicationId "com.koma.movie"
        minSdkVersion defaultMinSdkVersion as Integer
        targetSdkVersion defaultTargetSdkVersion as String
        versionCode appVersionCode as Integer
        versionName appVersionName as String

        multiDexEnabled true

        manifestPlaceholders = [ADMOB_APP_ID: "${ADMOB_APP_ID}"]

        resValue "string", "ad_unit_id", "$ad_unit_id"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            resValue "string", "ad_unit_id", "$release_ad_unit_id"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.multidex:multidex:$multidexVersion"

    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hiltJetpackVersion"
    kapt "androidx.hilt:hilt-compiler:$hiltJetpackVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$viewpagerVersion"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$interceptorVersion"

    implementation "pub.devrel:easypermissions:$permissionVersion"

    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"

    // Firebase
    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseAuthVersion"
    implementation "com.google.firebase:firebase-database-ktx:$firebaseDatabaseVersion"
    implementation "com.google.firebase:firebase-ads:$firebaseAdsVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlinxCoroutinesPlayServicesVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation project(':commonlibrary')
    implementation project(':movielibrary')
    implementation project(':tvlibrary')
    implementation project(':peoplelibrary')
    implementation project(':networklibrary')

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"

    testImplementation "androidx.room:room-testing:$roomVersion"
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500"
    }
}
